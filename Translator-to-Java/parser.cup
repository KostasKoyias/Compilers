// Package and Import Specifications
import java_cup.runtime.*;

// Usercode Components
parser code {:
    // Connect this parser to a scanner!
    Scanner s;
    Parser(Scanner s){ this.s=s; }
:}

// define how to connect to the scanner! 
scan with {: return s.next_token(); :};

/* Symbol Lists */

// Terminals (tokens returned by the scanner).
terminal            PLUS, LPAREN, RPAREN, LBRACK, RBRACK, COMMA, PREFIX, SUFFIX, IF, ELSE;
terminal String     IDENTIFIER, STRING_LITERAL;

//  Non terminals
non terminal        prog, def, defs, call, call_list, calls, args, arglist, comma_list, block, exp, str, if_else, cond, op;

// Precedence Declarations
precedence left PREFIX, SUFFIX; // low  - higher in the tree 
precedence left PLUS;           // high - deeper in the tree

/*** The Grammar Rules ***/
start with prog;

// a programm is a mix of definitions and function calls
prog ::= defs:ds calls:cs  {: System.out.println("public class Main{\n\tpublic static void main(String[] args){\n" + cs + "\t}\n" + ds + "\n}"); :}
;
// calls section is either empty or it contains a function call list, new-line seperated
calls ::= call:c calls:cs          {: RESULT = "\t\tSystem.out.println(" + ((String)c).replaceAll("String ","") + ");\n" + cs; :} 
        | /* empty */              {: RESULT = ""; :} 
;
// a call is of type identifier arguments e.g foo(arg0, arg1)
call ::= IDENTIFIER:id args:a      {: RESULT = id + a; :}
;
// arguments are of type (argument_list)
args ::= LPAREN arglist:al RPAREN  {: RESULT = "(" + al + ")"; :}    
;
// the argument list can either be empty or a non-empty comma seperated list of expressions
arglist ::= comma_list : c         {: RESULT = c; :}                        
        | /* empty */              {: RESULT = ""; :}                               
;
// a non-empty list of identifiers has either 1 element or a series of comma seperated expressions, all of them are eventually evaluated in a String
comma_list ::= comma_list:cl COMMA exp:e      {: RESULT = cl + ", " + "String " + e; :}
            | exp:e                           {: RESULT = "String " + e; :}
;
// a list of definitions is newline-seperated
defs ::= defs:ds def:d          {: RESULT = d + "\n" + ds; :}
        | /*empty*/             {: RESULT = ""; :}
;
// a function definition consists of an identifier and an argument list(like a call) and a code_block
def ::= call:c block:b          {: RESULT = "\tpublic static String " + c + b; :}
;
// a code_block is a bracket surrounded SINGLE expression
block ::= LBRACK exp:e RBRACK {: RESULT = "{\n\t\treturn " + e + ";\n\t}"; :}
;
// an expression returns a string by either producing an operation between two expression or returning it right away
exp ::= exp:e1 PLUS exp:e2      {: RESULT = e1 + " + " + e2; :}
    | str:s                     {: RESULT = s; :}
;
// a string can be an id or a string_literal, it can also be the return value of a function_call or of an if/else statement
str ::= IDENTIFIER:id           {: RESULT = id; :}
     |  STRING_LITERAL:sl       {: RESULT = "\"" + sl + "\""; :}
     |  call:c                  {: RESULT = ((String)c).replaceAll("String ",""); :} 
     |  if_else:i               {: RESULT = i; :}
     |  LPAREN exp:e RPAREN     {: RESULT = "(" + e + ")"; :}
;
// a typical inline if/else statement using operators ? and :
if_else ::= IF LPAREN cond:c RPAREN exp:e1 ELSE exp:e2  {: RESULT = "(" + c + "?" + e1 + ":" + e2 + ")"; :} 
;
// a condition is a boolean operation between two expressions(Strings)
cond ::= exp:e1 op:o exp:e2       {: RESULT = "(" + e2 + ")." + o + "(" + e1 + ")"; :}
;
// this language supports two boolean operations:i)prefix and ii)suffix
op ::= PREFIX   {:RESULT = "startsWith"; :}
    |  SUFFIX   {:RESULT = "endsWith"; :}
;